// Souffle - A Datalog Compiler
// Copyright (c) 2021, The Souffle Developers. All rights reserved
// Licensed under the Universal Permissive License v 1.0 as shown at:
// - https://opensource.org/licenses/UPL
// - <souffle root>/licenses/SOUFFLE-UPL.txt

// define edge relation of a directed graph
.decl e(x:number, y:number)

e(1,2).
e(2,2).
e(2,3).
e(1,3).

// define shortest path relation
.decl sp(x:number, y:number, c:number) btree_delete

//
// subsumption
//
// A longer path is subsumed by a shorted path, i.e.,
// the longer path is discarded from the relation.
sp(x,y,c1) <= sp(x,y, c2) :-
  c2 <= c1.

//
// base case
//
sp(x,y,1) :-
  e(x,y).

//
// inductive case
//
sp(x,z,c + 1) :-
  sp(x,y,c),
  e(y,z).

.output sp
